 AD05PLI:PROCEDURE OPTIONS(MAIN);

  /* Ulazne datoteke. */

   /* Datoteka koja sadrzi pravila */
  DCL INFL1 FILE RECORD INPUT;
   /* Datoteka koja sadrzi redoslijed stranica */
  DCL INFL2 FILE RECORD INPUT;

  /* Kontrolni flagovi za kraj datoteke */
  DCL EOF1 BIT(1) INIT('0'b);   /* Kraj datoteke INFL1 */
  DCL EOF2 BIT(1) INIT('0'b);   /* Kraj datoteke INFL2 */

  /* Flag koji oznacava je li redoslijed ispravan prema pravilima */
  DCL VALIDAN BIT(1);

  /* Brojaci i pomocne varijable */
  DCL (I, J, K1, K2, K3, X, Y) FIXED BINARY(15) INIT(0);

   /* Indeksi pronadenih vrijednosti X i Y u redoslijedu */
  DCL (INDX_X, INDX_Y) FIXED BINARY(15) INIT(0);

   /* Unosi iz datoteka */

   /* Pravila iz datoteke INFL1 */
  DCL UNOS1(1176) CHAR(5);
   /* Redoslijed stranica iz INFL2 */
  DCL UNOS2(200) CHAR(80);

   /* Cist redoslijed bez zareza i razmaka */
  DCL UNOS2_CLEAN CHAR(80) VARYING;

  /* Broj brojeva u trenutnom redoslijedu */
  DCL N FIXED BINARY(15) INIT(0);

  /* Pomocne varijable za dohvacanje srednjeg clana */

   /* Indeks srednjeg clana */
  DCL MID_INDEX FIXED BINARY(15) INIT(0);
   /* Vrijednost srednjeg clana */
  DCL MID_PAGE  FIXED BINARY(15) INIT(0);

   /* Indeks srednjeg clana - prepravljeni*/
  DCL MID_INDEX2 FIXED BINARY(15) INIT(0);
   /* Vrijednost srednjeg clana - prepravljeni */
  DCL MID_PAGE2  FIXED BINARY(15) INIT(0);

   /* Ukupna suma srednjih clanova */
  DCL SUMA  FIXED BINARY(15) INIT(0);
   /* Suma za zbroj svih prepravljenih nizova */
  DCL SUMA2 FIXED BINARY(15) INIT(0);

  /* Matrica za pohranu pravila (prvo i drugo pravilo) */
  DCL PRAVILA(1176,2) FIXED BINARY(31);

  /* Privremeni niz koji pohranjuje redoslijed stranica */
  DCL REDOSLJED(25) FIXED BINARY(31);

  /* Obrada zavrsetka datoteka */
  ON ENDFILE(INFL1) EOF1='1'b;
  ON ENDFILE(INFL2) EOF2='1'b;

  /* Otvaranje datoteka */
  OPEN FILE(INFL1),
       FILE(INFL2);

  /* Ucitavanje pravila iz INFL1 */
  FILE1:DO I = 1 TO 2000;
   READ FILE(INFL1) INTO(UNOS1(I));
   IF (EOF1='1'b) THEN LEAVE FILE1;

   /* Pretvorba pravila iz char u integer */
   PRAVILA(I,1) = BINARY(SUBSTR(UNOS1(I),1,2));
   PRAVILA(I,2) = BINARY(SUBSTR(UNOS1(I),4,2));

  END FILE1;

  /* Ucitavanje redoslijeda iz INFL2 */
  FILE2:DO I = 1 TO 200;
   READ FILE(INFL2) INTO(UNOS2(I));
  END FILE2;

  /* Obrada svakog redoslijeda u INFL2 */
  POPL_REDOSLJED:DO I = 1 TO DIM(UNOS2);

   UNOS2_CLEAN = ''; /* Resetiranje stringa */
   VALIDAN = '1'b;   /* Resetiranje flag-a valjanosti */

   /* ciscenje redoslijeda (uklanjanje zareza i razmaka) */
   UNOS2_CLEAN = SCRUBOUT(SCRUBOUT(UNOS2(I), ','), ' ');

   /* Broj elemenata u trenutnom redoslijedu */
   N = (LENGTH(UNOS2_CLEAN)/2);

   /* Popunjavanje niza REDOSLJED */
   DO J = 1 TO N;
    REDOSLJED(J) = BINARY(SUBSTR(UNOS2_CLEAN, (((J-1)*2)+1), 2));
   END;

   X,Y = 0; /* Resetiranje privremenih varijabli */

   /* Usporedba pravila s redoslijedom */
   PROVJERA:DO K1 = 1 TO 1176;

    INDX_X = 0;
    INDX_Y = 0;

    X = PRAVILA(K1,1);
    Y = PRAVILA(K1,2);

   /* Pronalazak indeksa elemenata X i Y unutar trenutnog redoslijeda */
    DO K2 = 1 TO N;
     IF (REDOSLJED(K2) = X) THEN INDX_X = K2;
     IF (REDOSLJED(K2) = Y) THEN INDX_Y = K2;
    END;

    /* Ako su oba broja pronadena u redoslijedu */
    IF (INDX_X > 0 & INDX_Y > 0) THEN DO;
        /* Ako X dolazi poslije Y, redoslijed nije valjan */
        IF (INDX_X > INDX_Y) THEN DO;
          VALIDAN = '0'b;
          LEAVE PROVJERA;
        END;
    END;

   END PROVJERA;

   /* 2. dio - Ako redosljed nije validan, treba ga prepraviti i dodati
               njgov srednji clan u SUMU2. */
   IF (VALIDAN = '0'b) THEN DO; 
    DO K2 = 1 TO (N-1);
     DO K3 = 1 TO (N-K2);
      /* Ako clanovi nisu u pravilnom redosljedu */
       DO K1 = 1 TO 1176;
        IF (PRAVILA(K1,1) = REDOSLJED(K3+1) & 
            PRAVILA(K1,2) = REDOSLJED(K3)) THEN DO; 

          X = REDOSLJED(K3);
          REDOSLJED(K3) = REDOSLJED(K3+1);
          REDOSLJED(K3+1) = X;  

        END;
       END;
     END;
    END;
    
    /* Nakon sortiranja, ponovo racunaj srednju stranicu */
    MID_INDEX2 = (N + 1) / 2;
    MID_PAGE2 = REDOSLJED(MID_INDEX2);
    SUMA2 = SUMA2 + MID_PAGE2;  
   END; 

   /* Ako je redoslijed valjan, obradujemo ga dalje */
   IF (VALIDAN = '1'b) THEN DO;
       /* Racunanje indeksa srednje stranice */
      MID_INDEX = (N + 1) / 2;
       /* Dohvacanje vrijednosti srednje stranice */
      MID_PAGE = REDOSLJED(MID_INDEX);
       /* Zbrajanje srednje stranice u ukupnu sumu */
      SUMA = SUMA + MID_PAGE;
       /* Ispis validnog niza i srednje stranice */
      /*PUT SKIP EDIT('STRANICE: ',UNOS2_CLEAN,' SREDINA: ',
                   SCRUBOUT(CHAR(MID_PAGE), ' '))(A);*/
   END;

  END POPL_REDOSLJED;

  /* Ispis ukupne sume srednjih stranica */
  PUT SKIP EDIT('Tocni:',SUMA)(A);
  PUT SKIP EDIT('Netocni:',SUMA2)(A);

  /* Zatvaranje datoteka */
  CLOSE FILE(INFL1),
        FILE(INFL2);

 END AD05PLI;
