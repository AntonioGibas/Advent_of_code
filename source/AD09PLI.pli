 AD09PLI: PROCEDURE OPTIONS(MAIN);
  DCL INFL FILE RECORD INPUT;
  DCL UNOS CHAR(20000) INIT('');
  DCL (I, J, K) FIXED BINARY(31) INIT(0);
  DCL CUR_NUM CHAR(1) INIT('');
  DCL CHR_NUM_BIN FIXED BINARY(31) INIT(0);
  DCL BLOCK_COUNT FIXED BINARY(31) INIT(0);
  DCL FILE_ID FIXED BINARY(31) INIT(0);
  DCL SUMA FIXED DECIMAL(15) INIT(0);
  DCL MNZTLJ FIXED DECIMAL(15) INIT(0);
  DCL DISK_MAP(100000) FIXED BINARY(31) INIT((100000)-1);
  DCL TOTAL_SIZE FIXED BINARY(31) INIT(0);
  DCL CURRENT_POS FIXED BINARY(31) INIT(0);
  DCL MAX_FILE_ID FIXED BINARY(31) INIT(0);
  DCL FILE_SIZES(0:100000) FIXED BINARY(31) INIT((100001)0);
  DCL FILE_START_POS(0:100000) FIXED BINARY(31) INIT((100001)0);
  DCL MAX_BLOCKS FIXED BINARY(31) INIT(100000);
  DCL FREE_START FIXED BINARY(31);
  DCL FREE_SIZE FIXED BINARY(31);
  DCL FOUND_SPACE BIT(1);
  
  OPEN  FILE(INFL);
  READ  FILE(INFL) INTO(UNOS);
  CLOSE FILE(INFL);

  PUT SKIP EDIT('Procesiranje input stringa od ', LENGTH(TRIM(UNOS)),
                ' znakova...')(A);

  CURRENT_POS = 1;
  FILE_ID = 0;
  BLOCK_COUNT = 0;

  PARSE:DO I = 1 TO LENGTH(UNOS);
    IF SUBSTR(UNOS,I,1) = ' ' THEN LEAVE PARSE;
    CUR_NUM = SUBSTR(UNOS, I, 1);
    IF CUR_NUM >= '0' & CUR_NUM <= '9' THEN DO;
      CHR_NUM_BIN = DECIMAL(CUR_NUM);
    END;
    ELSE LEAVE PARSE;

    IF CURRENT_POS + CHR_NUM_BIN > MAX_BLOCKS THEN DO;
      PUT SKIP EDIT('GRESKA: Prekoracene granice memorije na poziciji ', 
                    CURRENT_POS)(A);
      GOTO KRAJ;
    END;

    IF MOD(BLOCK_COUNT, 2) = 0 THEN DO; 
      FILE_SIZES(FILE_ID) = CHR_NUM_BIN;
      FILE_START_POS(FILE_ID) = CURRENT_POS;
      
      DO J = 1 TO CHR_NUM_BIN;
        DISK_MAP(CURRENT_POS) = FILE_ID;
        CURRENT_POS = CURRENT_POS + 1;
      END;
      FILE_ID = FILE_ID + 1;
    END;
    ELSE DO; 
      DO J = 1 TO CHR_NUM_BIN;
        DISK_MAP(CURRENT_POS) = -1;
        CURRENT_POS = CURRENT_POS + 1;
      END;
    END;
    BLOCK_COUNT = BLOCK_COUNT + 1;
  END PARSE;

  TOTAL_SIZE = CURRENT_POS - 1;
  MAX_FILE_ID = FILE_ID - 1;

  PUT SKIP EDIT('Ukupno blokova: ', TOTAL_SIZE)(A);
  PUT SKIP EDIT('Ukupno datoteka: ', FILE_ID)(A);
  PUT SKIP EDIT('Pocinje kompaktiranje cijelih datoteka...')(A);
  
  COMPACT_FILES:DO I = MAX_FILE_ID TO 0 BY -1;
    DCL CURRENT_FILE_START FIXED BINARY(31);
    DCL CURRENT_FILE_SIZE FIXED BINARY(31);
    
    CURRENT_FILE_START = FILE_START_POS(I);
    CURRENT_FILE_SIZE = FILE_SIZES(I);
    
    IF CURRENT_FILE_SIZE = 0 THEN ITERATE COMPACT_FILES;
    
    FOUND_SPACE = '0'B;
    
    FIND_SPACE:DO J = 1 TO CURRENT_FILE_START - 1;
      IF DISK_MAP(J) = -1 THEN DO;
        FREE_START = J;
        FREE_SIZE = 0;
        
        COUNT_FREE:DO K = J TO TOTAL_SIZE;
          IF DISK_MAP(K) = -1 THEN FREE_SIZE = FREE_SIZE + 1;
          ELSE LEAVE COUNT_FREE;
        END COUNT_FREE;
        
        IF FREE_SIZE >= CURRENT_FILE_SIZE THEN DO;
          DO K = CURRENT_FILE_START TO (CURRENT_FILE_START + 
              CURRENT_FILE_SIZE - 1);
            DISK_MAP(K) = -1;
          END;
          
          FILE_START_POS(I) = FREE_START;
          DO K = FREE_START TO FREE_START + CURRENT_FILE_SIZE - 1;
            DISK_MAP(K) = I;
          END;
          
          FOUND_SPACE = '1'B;
          LEAVE FIND_SPACE;
        END;
        
        J = J + FREE_SIZE - 1; 
      END;
    END FIND_SPACE;
    
  END COMPACT_FILES;

  SUMA = 0;
  DO I = 1 TO TOTAL_SIZE;
    IF DISK_MAP(I) >= 0 THEN DO;
      MNZTLJ = DISK_MAP(I); 
      SUMA = SUMA + (MNZTLJ * (I - 1)); 
    END;
  END;

  PUT SKIP EDIT('UKUPNA SUMA: ', SUMA)(A);

  KRAJ:

 END AD09PLI;