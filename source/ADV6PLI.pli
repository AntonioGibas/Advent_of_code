 ADV6PLI:PROCEDURE OPTIONS(MAIN);

   /* Ulazna datoteka s pocetnom pozicijom*/
  DCL INFL FILE RECORD INPUT;
   /* Izlazna datoteka za zapisivanje rezultata */
  DCL OUTFL FILE RECORD OUTPUT; 

   /* Varijabla za oznacavanje kraja datoteke */
  DCL EOF BIT(1) INIT('0'b); 
   /* Varijabla za oznacavanje kraja simulacije */
  DCL EOG BIT(1) INIT('0'b); 

   /* Koordinate pozicija (X, Y) */
  DCL (I, J, X, Y) FIXED BINARY(15) INIT(0); 
   /* Nova koordinata pozicije */
  DCL (NEW_X, NEW_Y) FIXED BINARY(15) INIT(0); 
   /* Smjer kretanja (0: gore, 1: desno, 2: dolje, 3: lijevo) */
  DCL DIRECTION FIXED BINARY(15) INIT(0); 
   /* Broj posjecenih pozicija */
  DCL BR_POZ FIXED BINARY(31) INIT(0); 
   /* Oznaka posjecenih pozicija na mrezi */
  DCL VISITED(130,130) BIT(1) INIT((130*130)'0'b); 
   /* Oznaka pozicija koje mogu tvoriti petlje */
  DCL LOOP_POS(130,130) BIT(1) INIT((130*130)'0'b); 
   /* Broj mogucih pozicija za prepreke */
  DCL OBSTACLE_COUNT FIXED BINARY(31) INIT(0); 

   /* Ulazni podaci u obliku stringova */
  DCL UNOS(130) CHAR(130);        
   /* Rekord za izlaznu datoteku */
  DCL OUTFL_RECORD CHAR(130);     
   /* Mreza za simulaciju */
  DCL GRID(130,130) CHAR(1);      
   /* Mreza putanja */
  DCL PATH_GRID(130,130) CHAR(1); 
   /* Originalna mreza koja se koristi za inicijalizaciju */
  DCL ORIG_GRID(130,130) CHAR(1); 

  /* Simulacijske varijable */

   /* Simulacijska koordinata X */
  DCL SIM_X FIXED BINARY(15);        
   /* Simulacijska koordinata Y */
  DCL SIM_Y FIXED BINARY(15);        
   /* Simulacijski smjer */
  DCL SIM_DIR FIXED BINARY(15);      
   /* Oznaka posjecenih pozicija i smjerova u simulaciji */
  DCL VISITED_SIM(130,130,4) BIT(1); 
   /* Oznaka da li je pozicija u petlji */
  DCL IN_LOOP BIT(1);                
   /* Privremena mreza za simulaciju */
  DCL TEMP_GRID(130,130) CHAR(1);    

  /* Pocetna pozicija i smjer */
  DCL (START_X, START_Y, START_DIR) FIXED BINARY(15) INIT(0); 

  ON ENDFILE(INFL) EOF='1'b; /* Oznaka kraja ulazne datoteke */

  OPEN FILE(INFL); /* Otvaranje ulazne datoteke */
  OPEN FILE(OUTFL); /* Otvaranje izlazne datoteke */
  
  /* citanje ulazne datoteke */
  RECORD_IN:DO I = 1 TO 130;
    READ FILE(INFL) INTO(UNOS(I));
  END RECORD_IN;

  /* Inicijalizacija mreza i pronalazak pocetne pozicije */
  PPLATE_GRID:DO I = 1 TO 130;
    DO J = 1 TO 130;
       /* Kopiranje podataka u originalnu mrezu */
      ORIG_GRID(I,J) = SUBSTR(UNOS(I), J, 1); 
       /* Kopiranje u mrezu za simulaciju */
      GRID(I,J) = ORIG_GRID(I,J); 
       /* Kopiranje u mrezu putanje */
      PATH_GRID(I,J) = GRID(I,J); 
     
      /* Pronalazenje pocetne pozicije */
      IF GRID(I,J) = '^' THEN DO; /* Smjer gore */
        START_X = I;
        START_Y = J;
        START_DIR = 0; /* Smjer gore */
        X = I;
        Y = J;
        DIRECTION = 0;
      END;
      ELSE IF GRID(I,J) = '>' THEN DO; /* Smjer desno */
        START_X = I;
        START_Y = J;
        START_DIR = 1; /* Smjer desno */
        X = I;
        Y = J;
        DIRECTION = 1;
      END;
      ELSE IF GRID(I,J) = 'v' THEN DO; /* Smjer dolje */
        START_X = I;
        START_Y = J;
        START_DIR = 2; /* Smjer dolje */
        X = I;
        Y = J;
        DIRECTION = 2;
      END;
      ELSE IF GRID(I,J) = '<' THEN DO; /* Smjer lijevo */
        START_X = I;
        START_Y = J;
        START_DIR = 3; /* Smjer lijevo */
        X = I;
        Y = J;
        DIRECTION = 3;
      END;
    END;
  END PPLATE_GRID;

  /* Oznacavanje pocetne pozicije kao posjecene */
  VISITED(X,Y) = '1'b; /* Oznaci kao posjeceno */
  PATH_GRID(X,Y) = 'X'; /* Oznaka putanje */
  GRID(X,Y) = '.'; /* Oznaka slobodne pozicije */
  BR_POZ = 1; /* Pocetni broj posjecenih pozicija */

  /* Glavna simulacija */
  PLAY:DO UNTIL(EOG='1'b); /* Glavna petlja simulacije */
    /* Izracun nove pozicije */
    SELECT(DIRECTION);
      WHEN(0) DO; /* Gora */
        NEW_X = X - 1;
        NEW_Y = Y;
      END;
      WHEN(1) DO; /* Desno */
        NEW_X = X;
        NEW_Y = Y + 1;
      END;
      WHEN(2) DO; /* Dolje */
        NEW_X = X + 1;
        NEW_Y = Y;
      END;
      WHEN(3) DO; /* Lijevo */
        NEW_X = X;
        NEW_Y = Y - 1;
      END;
    END;
    
    /* Provjera da li je nova pozicija izvan granica */
    IF (NEW_X < 1   | 
        NEW_X > 130 | 
        NEW_Y < 1   | 
        NEW_Y > 130) THEN DO;
      EOG = '1'b; /* Kraj simulacije */
      LEAVE PLAY; /* Izlaz iz glavne petlje */
    END;
    
    /* Provjera da li je nova pozicija prepreka */
    IF GRID(NEW_X, NEW_Y) = '#' THEN DO; /* Prepreka */
      DIRECTION = MOD(DIRECTION + 1, 4); /* Okreni desno */
    END;
    ELSE DO;
      X = NEW_X;
      Y = NEW_Y;
      
      /* Oznaci poziciju kao posjecenu */
      IF VISITED(X,Y) = '0'b THEN DO;
        VISITED(X,Y) = '1'b;
        BR_POZ = BR_POZ + 1;
      END;
      
      /* Azuriraj mreze */
      GRID(X,Y) = 'X';
      PATH_GRID(X,Y) = 'X';
    END;
  END PLAY;
  
  /* Dio 2: Pronadi pozicije za stvaranje petlji */
  FIND_LOOP_POS: DO I = 1 TO 130;
    DO J = 1 TO 130;
      /* Provjeri je li pozicija prazna i nije pocetna */
      IF ORIG_GRID(I,J) = '.' & 
         (I <> START_X | J <> START_Y) THEN DO;
        /* Kopiraj ORIG_GRID u TEMP_GRID i dodaj prepreku */
        DO X = 1 TO 130; 
         DO Y = 1 TO 130; 
          TEMP_GRID(X,Y) = ORIG_GRID(X,Y); 
         END; 
        END;
        TEMP_GRID(I,J) = '#'; /* Dodaj prepreku */
        
        /* Resetiraj simulacijske varijable */
        SIM_X = START_X;
        SIM_Y = START_Y;
        SIM_DIR = START_DIR;
        VISITED_SIM = '0'b;
        IN_LOOP = '0'b;
        
        /* Pokreni simulaciju */
        SIMULATE: DO WHILE(IN_LOOP = '0'b);
           /* Koordinata X u simulaciji */
          DCL SIM_NEW_X FIXED BINARY(15); 
           /* Koordinata Y u simulaciji */
          DCL SIM_NEW_Y FIXED BINARY(15); 
        
          SELECT(SIM_DIR);
            WHEN(0) DO; /* Gora */
              SIM_NEW_X = SIM_X - 1;
              SIM_NEW_Y = SIM_Y;
            END;
            WHEN(1) DO; /* Desno */
              SIM_NEW_X = SIM_X;
              SIM_NEW_Y = SIM_Y + 1;
            END;
            WHEN(2) DO; /* Dolje */
              SIM_NEW_X = SIM_X + 1;
              SIM_NEW_Y = SIM_Y;
            END;
            WHEN(3) DO; /* Lijevo */
              SIM_NEW_X = SIM_X;
              SIM_NEW_Y = SIM_Y - 1;
            END;
          END;
        
          /* Provjeri da li je nova pozicija izvan granica */
          IF SIM_NEW_X < 1   | 
             SIM_NEW_X > 130 | 
             SIM_NEW_Y < 1   | 
             SIM_NEW_Y > 130 THEN DO;
            LEAVE SIMULATE;
          END;
        
          /* Provjera da li je nova pozicija prepreka */
          IF TEMP_GRID(SIM_NEW_X, SIM_NEW_Y) = '#' THEN DO;
            /* Okreni desno */
            SIM_DIR = MOD(SIM_DIR + 1, 4);
            GOTO SIMULATE_CONTINUE;
          END;
        
          /* Kreci se naprijed */
          SIM_X = SIM_NEW_X;
          SIM_Y = SIM_NEW_Y;
        
          /* Provjeri je li pozicija i smjer vec posjecen */
          IF VISITED_SIM(SIM_X, SIM_Y, SIM_DIR + 1) = '1'b THEN DO;
            IN_LOOP = '1'b; /* Detektirana petlja */
            LOOP_POS(I,J) = '1'b;
            OBSTACLE_COUNT = OBSTACLE_COUNT + 1;
          END;
          ELSE DO;
            VISITED_SIM(SIM_X, SIM_Y, SIM_DIR + 1) = '1'b;
          END;
        
        SIMULATE_CONTINUE: END SIMULATE;
        
        /* Vrati originalnu mrezu */
        TEMP_GRID(I,J) = '.';
      END;
    END;
  END FIND_LOOP_POS;
  
  /* Ispis rezultata */
   /* Ispis broja posjecenih pozicija */
  PUT SKIP LIST('Broj posjecenih pozicija:', BR_POZ);
  /* Ispis broja mogucih prepreka */
  PUT SKIP EDIT('Broj mogucih pozicija za prepreku:',OBSTACLE_COUNT)(A); 

  CLOSE FILE(INFL); /* Zatvaranje ulazne datoteke */
  CLOSE FILE(OUTFL); /* Zatvaranje izlazne datoteke */

 END ADV6PLI;
