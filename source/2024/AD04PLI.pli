 AD04PLI:PROCEDURE OPTIONS(MAIN);

    DCL INFL FILE RECORD INPUT;
    DCL EOF BIT(1) INIT('0'b);
    DCL UNOS(140) CHAR(140);
    DCL GRID(140,140) CHAR(1);

    DCL TARGET CHAR(4) INIT('XMAS');

    DCL (I, J, X, Y) FIXED BINARY(31) INIT(0);
     /* Brojac za instance rijeci pronadjene u matrici */    
    DCL WRDS FIXED BINARY(15) INIT(0);
     /* Brojac za cross rijeci po dijagonalama */
    DCL CROSS FIXED BINARY(15) INIT(0);

    ON ENDFILE(INFL) EOF='1'b;

    REINIT WRDS;

    OPEN FILE(INFL);

     /* Popunjavanje niza iz ulazne datoteke. */
    PPLATE_UNOS:DO I = 1 TO 140;
     READ FILE(INFL) INTO(UNOS(I));
    END PPLATE_UNOS;

     /* Popunjavanje matrice s nizom. */
    PPLATE_GRID:DO I = 1 TO 140;
     DO J = 1 TO 140;
      GRID(I,J) = SUBSTR(UNOS(I), J, 1);
     END;
    END PPLATE_GRID;

    /* Pretraga po X osi (vodoravno): */
    X_OS:DO X = 1 TO 140;
     DO Y = 1 TO 137; /* Do 137 stupca se moze ocitati po 4 znaka */
       
        /* s desna na lijevo */
       IF (GRID(X,Y)   || 
           GRID(X,Y+1) || 
           GRID(X,Y+2) || 
           GRID(X,Y+3) = TARGET) THEN WRDS += 1;      
       
        /* s lijeva na desno */
       IF (GRID(X,Y+3) || 
           GRID(X,Y+2) || 
           GRID(X,Y+1) || 
           GRID(X,Y) = TARGET) THEN WRDS += 1;  

     END;
    END X_OS;

    /* Pretraga po Y osi (okomito): */
    Y_OS:DO Y = 1 TO 140;
     DO X = 1 TO 137; 

       /* s desna na lijevo */
      IF (GRID(X,Y)   ||
          GRID(X+1,Y) ||
          GRID(X+2,Y) || 
          GRID(X+3,Y) = TARGET) THEN WRDS += 1;

        /* s lijeva na desno */
       IF (GRID(X+3,Y) || 
           GRID(X+2,Y) || 
           GRID(X+1,Y) || 
           GRID(X,Y) = TARGET) THEN WRDS += 1;  
     END;
    END Y_OS;

    /* Pretraga po donjoj desnoj dijagonali "\": */
    DIJ1:DO X = 1 TO 137;
     DO Y = 1 TO 137;

       /* s desna na lijevo */
      IF (GRID(X,Y)     ||
          GRID(X+1,Y+1) ||
          GRID(X+2,Y+2) || 
          GRID(X+3,Y+3) = TARGET) THEN WRDS += 1;    

      /* s lijeva na desno */
      IF (GRID(X+3,Y+3) ||
          GRID(X+2,Y+2) ||
          GRID(X+1,Y+1) || 
          GRID(X,Y) = TARGET) THEN WRDS += 1; 

     END;
    END DIJ1;

    /* Pretraga po donjoj lijevoj dijagonali "/": */
    DIJ2:DO X = 1 TO 137;
     DO Y = 4 TO 140; /* mora biti barem 4 da ne izlazi iz granica */

       /* s desna na lijevo */
      IF (GRID(X,Y)     ||
          GRID(X+1,Y-1) ||
          GRID(X+2,Y-2) || 
          GRID(X+3,Y-3) = TARGET) THEN WRDS += 1;      

      /* s lijeva na desno */
      IF (GRID(X+3,Y-3) ||
          GRID(X+2,Y-2) ||
          GRID(X+1,Y-1) || 
          GRID(X,Y) = TARGET) THEN WRDS += 1;   
     END;
    END DIJ2;


       /* Pretraga iskrizanih dijagonala s trazenom rijeci MAS gdje je 
      slovo "A" u sredini iskrizanih dijagonala. */
    CROSS_1:DO X = 2 TO 139; /* A ne moze biti na rubovima matrice. */
     DO Y = 2 TO 139;

    IF GRID(X,Y) = "A" THEN DO;
      /* Kombinacija 1: M-A-S (gore lijevo do dolje desno) i 
         M-A-S (gore desno do dolje lijevo) */
      IF (GRID(X-1,Y-1) = 'M' & 
          GRID(X+1,Y+1) = 'S' & 
          GRID(X-1,Y+1) = 'M' & 
          GRID(X+1,Y-1) = 'S') THEN CROSS += 1;

      /* Kombinacija 2: M-A-S (gore lijevo do dolje desno) i 
         S-A-M (gore desno do dolje lijevo) */
      IF (GRID(X-1,Y-1) = 'M' & 
          GRID(X+1,Y+1) = 'S' & 
          GRID(X-1,Y+1) = 'S' & 
          GRID(X+1,Y-1) = 'M') THEN CROSS += 1;

      /* Kombinacija 3: S-A-M (gore lijevo do dolje desno) i 
         M-A-S (gore desno do dolje lijevo) */
      IF (GRID(X-1,Y-1) = 'S' & 
          GRID(X+1,Y+1) = 'M' & 
          GRID(X-1,Y+1) = 'M' & 
          GRID(X+1,Y-1) = 'S') THEN CROSS += 1;

      /* Kombinacija 4: S-A-M (gore lijevo do dolje desno) i 
         S-A-M (gore desno do dolje lijevo) */
      IF (GRID(X-1,Y-1) = 'S' & 
          GRID(X+1,Y+1) = 'M' & 
          GRID(X-1,Y+1) = 'S' & 
          GRID(X+1,Y-1) = 'M') THEN CROSS += 1;
    END;
     END;
    END CROSS_1;

    CLOSE FILE(INFL);

    PUT SKIP EDIT(WRDS)(A);
    PUT SKIP EDIT(CROSS)(A);

 END AD04PLI;