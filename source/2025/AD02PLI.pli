 AD02PLI:PROCEDURE OPTIONS(MAIN);
  DCL INFL FILE RECORD INPUT;
  DCL UNOS CHAR(511);
  DCL (I, LEN) FIXED BINARY(31);
  DCL (SUMA_PT1, SUMA_PT2) FIXED BINARY(63) INIT(0);
  
  OPEN FILE(INFL);
  READ FILE(INFL) INTO(UNOS);
  CLOSE FILE(INFL);
  
  LEN = LENGTH(TRIM(UNOS));
  I = 1;
  
  DO WHILE(I <= LEN);
   CALL PARSE_RANGE;
  END;
 
  PUT SKIP(2) EDIT('PART 1: ', SUMA_PT1)(A);
  PUT SKIP EDIT('PART 2: ', SUMA_PT2)(A);
 
  PARSE_RANGE: PROCEDURE;
   DCL (START_POS, END_POS, DASH_POS, ID1, ID2, N) FIXED BINARY(63);
   DCL ID_STR CHAR(20) VARYING;
   
   START_POS = I;
   DO WHILE(SUBSTR(UNOS,I,1) <> '-');
    I += 1;
   END;
   DASH_POS = I;
   I += 1;
   
   DO WHILE(I <= LEN & SUBSTR(UNOS,I,1) <> ',');
    I += 1;
   END;
   END_POS = I - 1;
   I += 1;
   
   ID1 = BINARY(SUBSTR(UNOS, START_POS, DASH_POS - START_POS));
   ID2 = BINARY(SUBSTR(UNOS, DASH_POS + 1, END_POS - DASH_POS));
   
   DO N = ID1 TO ID2;
    ID_STR = TRIM(CHAR(N));
    IF IS_REPEATING(ID_STR, 2) THEN SUMA_PT1 += N;
    IF IS_REPEATING(ID_STR, 0) THEN SUMA_PT2 += N;
   END;
  END PARSE_RANGE;
 
  IS_REPEATING: PROCEDURE(S, MIN_REPS) RETURNS(BIT(1));
   DCL S CHAR(20) VARYING;
   DCL MIN_REPS FIXED BINARY(31);
   DCL (LEN_S, PLEN, I, REPS) FIXED BINARY(31);
   DCL PATTERN CHAR(10) VARYING;
   
   LEN_S = LENGTH(S);
   
   DO PLEN = 1 TO LEN_S / 2;
    IF MOD(LEN_S, PLEN) <> 0 THEN ITERATE;
    
    REPS = LEN_S / PLEN;
    IF MIN_REPS > 0 & REPS <> MIN_REPS THEN ITERATE;
    
    PATTERN = SUBSTR(S, 1, PLEN);
    
    DO I = PLEN + 1 TO LEN_S BY PLEN;
     IF SUBSTR(S, I, PLEN) <> PATTERN THEN GO TO NEXT_PLEN;
    END;
    
    RETURN('1'b);
    
    NEXT_PLEN:
   END;
   
   RETURN('0'b);
  END IS_REPEATING;

 END AD02PLI;