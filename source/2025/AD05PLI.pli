 AD05PLI:PROCEDURE OPTIONS(MAIN);
  DCL RANGES FILE RECORD INPUT;
  DCL INGID  FILE RECORD INPUT; 
 
  DCL RASPON_LINE CHAR(31) VARYING;
  DCL ID_LINE CHAR(15);
 
  DCL RASPON_OD(186) FIXED BINARY(63);
  DCL RASPON_DO(186) FIXED BINARY(63); 
  DCL MERGED_OD(186) FIXED BINARY(63);
  DCL MERGED_DO(186) FIXED BINARY(63);
  
  DCL (FRESH_NUM, BR_MERGED) FIXED BINARY(15) INIT(0);
  DCL (SUMA, TEMP) FIXED BINARY(63) INIT(0);
  DCL (I, J, POS) FIXED BINARY(15);
 
  OPEN FILE(RANGES), FILE(INGID);  
  
  DO I = 1 TO 186;
   READ FILE(RANGES) INTO(RASPON_LINE);
   POS = INDEX(RASPON_LINE, '-');
   RASPON_OD(I) = BINARY(SUBSTR(RASPON_LINE, 1, POS - 1));
   RASPON_DO(I) = BINARY(SUBSTR(RASPON_LINE, POS + 1));
  END;
  
  CALL SORTIRAJ_RASPONE;
  CALL SPOJI_RASPONE;
  
  DO J = 1 TO 1000;
   READ FILE(INGID) INTO(ID_LINE);
   IF PROVJERI_ID(BINARY(ID_LINE)) THEN FRESH_NUM += 1;
  END;
  
  CLOSE FILE(RANGES), FILE(INGID);
  
  DO I = 1 TO BR_MERGED;
   SUMA += (MERGED_DO(I) - MERGED_OD(I) + 1);
  END;
  
  PUT SKIP EDIT('SVJEZIH: ', 
                SCRUBOUT(CHAR(FRESH_NUM),' '))(A);
  PUT SKIP EDIT('UKUPNO: ', 
                SCRUBOUT(CHAR(SUMA),' '))(A);
 
  SORTIRAJ_RASPONE:PROCEDURE;
   DO I = 1 TO 185;
    DO J = I + 1 TO 186;
     IF RASPON_OD(J) < RASPON_OD(I) THEN DO;
      TEMP = RASPON_OD(I);
      RASPON_OD(I) = RASPON_OD(J);
      RASPON_OD(J) = TEMP;
      
      TEMP = RASPON_DO(I);
      RASPON_DO(I) = RASPON_DO(J);
      RASPON_DO(J) = TEMP;
     END;
    END;
   END;
  END SORTIRAJ_RASPONE;
 
  SPOJI_RASPONE:PROCEDURE;
   BR_MERGED = 1;
   MERGED_OD(1) = RASPON_OD(1);
   MERGED_DO(1) = RASPON_DO(1);
   
   DO I = 2 TO 186;
    IF RASPON_OD(I) <= MERGED_DO(BR_MERGED) + 1 THEN DO;
     IF RASPON_DO(I) > MERGED_DO(BR_MERGED) THEN
      MERGED_DO(BR_MERGED) = RASPON_DO(I);
    END;
    ELSE DO;
     BR_MERGED += 1;
     MERGED_OD(BR_MERGED) = RASPON_OD(I);
     MERGED_DO(BR_MERGED) = RASPON_DO(I);
    END;
   END;
  END SPOJI_RASPONE;
 
  PROVJERI_ID:PROCEDURE(BIN_ID) RETURNS(BIT(1));
   DCL BIN_ID FIXED BINARY(63);
   DCL K FIXED BINARY(15);
   
   DO K = 1 TO BR_MERGED;
    IF BIN_ID >= MERGED_OD(K) & BIN_ID <= MERGED_DO(K) THEN
     RETURN('1'b);
   END;
   
   RETURN('0'b);
  END PROVJERI_ID;

 END AD05PLI;