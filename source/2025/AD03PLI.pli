 AD03PLI:PROCEDURE OPTIONS(MAIN);
    DCL UNOS(200) CHAR(100);
    DCL INFL FILE RECORD INPUT;
    DCL I FIXED BINARY(15) INIT(0);

    DCL (DIGIT1, DIGIT2) CHAR(1) INIT('');
    DCL CONCATENATED CHAR(2) INIT('');

    DCL SUMA FIXED DECIMAL(18,0) INIT(0);

    DCL DOBIVENI_BROJ_STR CHAR(12);

    OPEN FILE(INFL);
    I = 0;
    POPUNI_ARRAY:DO I = 1 TO 200;
       READ FILE(INFL) INTO(UNOS(I));
    END POPUNI_ARRAY;
    CLOSE FILE(INFL);

    SUMA = 0;

    OBRADA:DO I = 1 TO 200;

     DOBIVENI_BROJ_STR = FIND_BEST_12(UNOS(I));

     PUT SKIP EDIT('Red ', I, ': ', DOBIVENI_BROJ_STR)(A, F(3), A, A);

     SUMA = SUMA + DECIMAL(DOBIVENI_BROJ_STR, 15, 0);

    END OBRADA;

    PUT SKIP LIST('------------------------------');
    PUT SKIP EDIT('UKUPNA SUMA: ', SUMA)(A, F(20));

    FIND_BEST_12: PROCEDURE(DIGITS_RAW) RETURNS(CHAR(12));
     DCL DIGITS_RAW CHAR(100);
     DCL DIGITS CHAR(100) VARYING;

     DCL RESULT CHAR(12) VARYING INIT('');

     DCL (LEN, REMAINING, LIMIT) FIXED BINARY(15);
     DCL (LAST_POS, J) FIXED BINARY(15);

     DCL MAX_VAL FIXED BINARY(15);
     DCL CURR_VAL FIXED BINARY(15);

     DCL BEST_DIGIT CHAR(1);
     DCL BEST_POS FIXED BINARY(15);
     DCL K FIXED BINARY(15);

     DIGITS = TRIM(DIGITS_RAW);
     LEN = LENGTH(DIGITS);

     LAST_POS = 0;

     DO K = 1 TO 12;
      REMAINING = 12 - K;
      LIMIT = LEN - REMAINING;

      MAX_VAL = -1;
      BEST_DIGIT = '';

      SC4N: DO J = LAST_POS + 1 TO LIMIT;

           CURR_VAL = BINARY(SUBSTR(DIGITS, J, 1));

           IF CURR_VAL > MAX_VAL THEN DO;
              MAX_VAL = CURR_VAL;
              BEST_POS = J;
              BEST_DIGIT = SUBSTR(DIGITS, J, 1);

              IF MAX_VAL = 9 THEN LEAVE SC4N;
           END;
      END SC4N;

      RESULT = RESULT || BEST_DIGIT;

      LAST_POS = BEST_POS;
     END;

     RETURN(RESULT);

    END FIND_BEST_12;

 END AD03PLI;
