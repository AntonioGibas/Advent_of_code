 AD04PLI: PROCEDURE OPTIONS(MAIN);

  DCL UNOS(135) CHAR(135);
  DCL INFL FILE RECORD INPUT;
  DCL (I, J, X, Y, DX, DY, BROJAC) FIXED BINARY(31);
  DCL GRID(135,135) CHAR(1);
  DCL R_GRID(135,135) CHAR(1);
  DCL (DOSP_ROL, UKLONJENE_U_ITR, UKLONJENE_TOTAL) 
   FIXED BINARY(15) INIT(0);
  DCL ROLA CHAR(1) STATIC INIT('@');
  DCL PRAZAN CHAR(1) STATIC INIT('.');
 
  OPEN FILE(INFL);
  DO I = 1 TO 135;
   READ FILE(INFL) INTO(UNOS(I));
   DO J = 1 TO 135;
    GRID(I,J) = SUBSTR(UNOS(I),J,1);
    R_GRID(I,J) = GRID(I,J);
   END;
  END;
  CLOSE FILE(INFL);
 
  CALL BROJI_DOSTUPNE(GRID, DOSP_ROL);
  PUT SKIP EDIT('PART 1: ', DOSP_ROL)(A);
 
  DO WHILE('1'b);
   UKLONJENE_U_ITR = 0;
   DO X = 1 TO 135;
    DO Y = 1 TO 135;
     IF R_GRID(X,Y) = ROLA THEN DO;
      CALL BROJI_SUSJEDE(R_GRID, X, Y, BROJAC);
      IF BROJAC < 4 THEN DO;
       R_GRID(X,Y) = PRAZAN;
       UKLONJENE_U_ITR += 1;
      END;
     END;
    END;
   END;
   IF UKLONJENE_U_ITR = 0 THEN LEAVE;
   UKLONJENE_TOTAL += UKLONJENE_U_ITR;
  END;
 
  PUT SKIP EDIT('PART 2: ', UKLONJENE_TOTAL)(A);
 
  BROJI_DOSTUPNE: PROCEDURE(G, CNT);
   DCL G(135,135) CHAR(1);
   DCL CNT FIXED BINARY(15);
   DCL (X, Y, N) FIXED BINARY(31);
   CNT = 0;
   DO X = 1 TO 135;
    DO Y = 1 TO 135;
     IF G(X,Y) = ROLA THEN DO;
      CALL BROJI_SUSJEDE(G, X, Y, N);
      IF N < 4 THEN CNT += 1;
     END;
    END;
   END;
  END BROJI_DOSTUPNE;
 
  BROJI_SUSJEDE: PROCEDURE(G, X, Y, CNT);
   DCL G(135,135) CHAR(1);
   DCL (X, Y, CNT, DX, DY, NX, NY) FIXED BINARY(31);
   CNT = 0;
   DO DX = -1 TO 1;
    DO DY = -1 TO 1;
     IF DX = 0 & DY = 0 THEN ITERATE;
     NX = X + DX;
     NY = Y + DY;
     IF NX >= 1 & NX <= 135 & NY >= 1 & NY <= 135 THEN
      IF G(NX,NY) = ROLA THEN CNT += 1;
    END;
   END;
  END BROJI_SUSJEDE;

 END AD04PLI;