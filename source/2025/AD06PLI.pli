 AD06PLI: PROCEDURE OPTIONS(MAIN);

  DCL INFL FILE RECORD INPUT;
  DCL FILE_DATA(100) CHAR(32000) VARYING;
  DCL (I, R, C) FIXED BIN(31);
  DCL (NUM_ROWS, MAX_WIDTH) FIXED BIN(31) INIT(0);
  DCL START_COL FIXED BIN(31) INIT(0);
  DCL IN_PROBLEM BIT(1) INIT('0'B);
  DCL GRAND_TOTAL FIXED DECIMAL(31) INIT(0);
  DCL LINE_BUF CHAR(32000) VARYING;

  OPEN FILE(INFL);
  ON ENDFILE(INFL) GOTO FILE_READ_DONE;

  DO I = 1 TO 100;
     READ FILE(INFL) INTO(LINE_BUF);
     NUM_ROWS = I;
     FILE_DATA(I) = LINE_BUF;
     IF LENGTH(LINE_BUF) > MAX_WIDTH THEN MAX_WIDTH = LENGTH(LINE_BUF);
  END;

 FILE_READ_DONE:
  CLOSE FILE(INFL);

  DO C = 1 TO MAX_WIDTH + 1;
     IF CHECK_COLUMN(C) THEN DO;
        IF IN_PROBLEM = '0'B THEN DO;
           START_COL = C;
           IN_PROBLEM = '1'B;
        END;
     END;
     ELSE DO;
        IF IN_PROBLEM = '1'B THEN DO;
           CALL PROCESS_BLOCK(START_COL, C - 1);
           IN_PROBLEM = '0'B;
        END;
     END;
  END;

  PUT SKIP EDIT('GRAND TOTAL: ', GRAND_TOTAL)(A);

  CHECK_COLUMN: PROCEDURE(COL_IDX) RETURNS(BIT(1));
     DCL COL_IDX FIXED BIN(31);
     DCL RR FIXED BIN(31);
     
     DO RR = 1 TO NUM_ROWS;
        IF COL_IDX <= LENGTH(FILE_DATA(RR)) THEN DO;
           IF SUBSTR(FILE_DATA(RR), COL_IDX, 1) > ' ' THEN RETURN('1'B);
        END;
     END;
     RETURN('0'B);
  END CHECK_COLUMN;

 PROCESS_BLOCK: PROCEDURE(C_START, C_END);
   DCL (C_START, C_END) FIXED BIN(31);
   DCL CURR_C FIXED BIN(31);
   DCL RR FIXED BIN(31);
   DCL TEMP_STR CHAR(100) VARYING;
   DCL CHAR_NOW CHAR(1);
   DCL OPERATOR CHAR(1) INIT(' ');
   DCL NUMBERS(100) FIXED DECIMAL(31);
   DCL CNT FIXED BIN(15) INIT(0);
   DCL BLOCK_SUM FIXED DECIMAL(31);
   DCL K FIXED BIN(31);

   DO CURR_C = C_START TO C_END;
      IF CURR_C <= LENGTH(FILE_DATA(NUM_ROWS)) THEN DO;
         CHAR_NOW = SUBSTR(FILE_DATA(NUM_ROWS), CURR_C, 1);
         IF CHAR_NOW = '+' | CHAR_NOW = '*' THEN OPERATOR = CHAR_NOW;
      END;
   END;

   IF OPERATOR = ' ' THEN OPERATOR = '+';  

   DO CURR_C = C_END TO C_START BY -1;
      TEMP_STR = '';

      DO RR = 1 TO NUM_ROWS - 1;
         IF CURR_C <= LENGTH(FILE_DATA(RR)) THEN DO;
            CHAR_NOW = SUBSTR(FILE_DATA(RR), CURR_C, 1);

            IF CHAR_NOW >= '0' & CHAR_NOW <= '9' THEN
               TEMP_STR = TEMP_STR || CHAR_NOW;
         END;
      END;

      IF LENGTH(TEMP_STR) > 0 THEN DO;
         CNT += 1;
         NUMBERS(CNT) = DECIMAL(TEMP_STR);
      END;
   END;

   IF CNT > 0 THEN DO;
      BLOCK_SUM = NUMBERS(1);
      DO K = 2 TO CNT;
         IF OPERATOR = '+' THEN BLOCK_SUM += NUMBERS(K);
         ELSE BLOCK_SUM *= NUMBERS(K);
      END;

      GRAND_TOTAL += BLOCK_SUM;
   END;

 END PROCESS_BLOCK;


 END AD06PLI;